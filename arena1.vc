#define ARENA_1_RESPAWN_TIME	1500

void start()
{
	nPlayerEnt = EntitySpawn(30,26,"ghost.chr");
	SetPlayer(nPlayerEnt);
	HookEntityRender(nPlayerEnt, "PlayerRender");
	entity.speed[nPlayerEnt] = PLAYER_WALK_SPEED;
	
	sAreaName = "Arena 1";	

	//do clean up and call MapVC virtual functions
	InitMap(ARENA_MISSION_OFFSET + 1);
	
	SetLowObsZone(1);

	MusicSwitch("boss.mod", 15);
	
	bCanSave = 0;
	
	RemoveHook("DoBlur");

	bLoadingGame=0;
	
	ArenaStart(500, 5);
	
	ArenaAddSpawnPoint(42, 18);
	ArenaAddSpawnPoint(42, 34);
	ArenaAddSpawnPoint(19, 34);
	ArenaAddSpawnPoint(18, 18);
	
	AddHook("Respawner", 5);
}

void InitEnemies()
{
}

void InitLoot()
{
	CreateLoot(30*16, 22*16, "Medkit", 0, "medkit.png", "GetMedkitLootArena1", 0);
}

void InitDoors()
{
}

void InitLights()
{
	nGlobalLight = 125;

	//the light at players start
	AddLight(20*16, 18*16, 100, 100, LIGHT_TYPE_ROUND);
	AddLight(40*16, 18*16, 100, 100, LIGHT_TYPE_ROUND);
	
	AddLight(20*16, 33*16, 100, 100, LIGHT_TYPE_ROUND);
	AddLight(40*16, 33*16, 100, 100, LIGHT_TYPE_ROUND);
}

void InitAmbientSounds()
{
}

void InitAutoSave()
{
}

void ArenaRewards()
{
	string sArenaText;
	//rewards based on time spent
	if(vcTimer[TIMER_ENEMY_LOGIC].nTick > 3000 && !bArenaRewards[0])
	{
		sArenaText = "Survived for 30 second! +1 medkit!";
		lucentTextFade(160-(TextWidth(nTinyFont, sArenaText)/2), 80-(FontHeight(nTinyFont)/2), sArenaText, nTinyFont, RGB(255, 255, 255), 0);
		GetMedkitLoot();
		
		bArenaRewards[0] = 1;
	}

	if(vcTimer[TIMER_ENEMY_LOGIC].nTick > 6000 && !bArenaRewards[1])
	{
		sArenaText = "Survived for 60 seconds! Ammo refilled!";
		lucentTextFade(160-(TextWidth(nTinyFont, sArenaText)/2), 80-(FontHeight(nTinyFont)/2), sArenaText, nTinyFont, RGB(255, 255, 255), 0);
		GetPistol();
		GetShotgun();
		GetM4();
		
		bArenaRewards[1] = 1;
	}
}

void Respawner()
{
	//cycle through the loots in this map to see if they should be respawned
	int i;
	
	for(i=0;i<nNumLoot;i++)
	{
		if(Loots[i].nStolen == 1 && vcTimer[TIMER_ENEMY_LOGIC].nTick > nStealTimes[i] + ARENA_1_RESPAWN_TIME)
		{
			SoundSpawn();
			Loots[i].nStolen = 0;
		}
	}
}

void GetMedkitLootArena1()
{
	nStealTimes[nCurrentLoot] = vcTimer[TIMER_ENEMY_LOGIC].nTick;
	GetMedkitLoot();
}

void GetPistolLootArena1()
{
	nStealTimes[nCurrentLoot] = vcTimer[TIMER_ENEMY_LOGIC].nTick;
	GetPistol();
}