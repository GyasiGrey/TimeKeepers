void FreeAimWeapon()
{
	int bInRange;
	
	//free aim!
	if(!bAimingLastTick)
	{
		nFreeAimX = entity.x[nPlayerEnt];
		nFreeAimY = entity.y[nPlayerEnt];
		entity.speed[nPlayerEnt] = 0;
	}

	bInRange = (DistanceXY(entity.x[nPlayerEnt], entity.y[nPlayerEnt], nFreeAimX, nFreeAimY)  < Weapons[nEquipedWeapon].nRange);

	if(up)
	{
		if(bInRange)
			nFreeAimY-=2;
		else
			nFreeAimY+=2;
	}

	if(down)
	{
		if(bInRange)
			nFreeAimY+=2;
		else
			nFreeAimY-=2;
	}

	if(left)
	{
		if(bInRange)
			nFreeAimX-=2;
		else
			nFreeAimX+=2;
	}

	if(right)
	{
		if(bInRange)
			nFreeAimX+=2;
		else
			nFreeAimX-=2;

	}

	anims[nTargetPointerAnim].x = nFreeAimX;
	anims[nTargetPointerAnim].y = nFreeAimY;
}

void ShootWeapon()
{
	//fire weapon
	if(BUTTON_SHOOT)
	{
		if(nAmmo[nEquipedWeapon] == 0)
		{
			//no ammo, play the out of ammo sound
			//guards can hear the click
			MakeSound(nPlayerEnt, 50);
			SoundNoAmmo();
			UnpressButtons();
		}
		else
		{
			if(timer > nShotAt+Weapons[nEquipedWeapon].nRecharge)
			{
				if(strcmp(sEntitiesInRange, "")!=0)
				{
					//let the player hold down the fire button on automatic weapons
					if(Weapons[nEquipedWeapon].nType != WEAPON_TYPE_AUTO)
						UnpressButtons();

					FireWeapon(nPlayerEnt, Enemies[nTargetIndex].nEntity);
				}
			}
		}
	}
}

int nDeltaX;
int nDeltaY;
int nDeltaXDecay;
int nDeltaYDecay;
int nThrownAnim;

void ThrowGrenade()
{
	if(nAmmo[nEquipedWeapon] == 0)
	{
		//no ammo, play the out of ammo sound
		//guards can hear the click
		MakeSound(nPlayerEnt, 50);
		SoundNoAmmo();
		UnpressButtons();
	}
	else
	{
		if(timer > nShotAt+Weapons[nEquipedWeapon].nRecharge)
		{
			nDeltaX = oF_divFF(oF_Whole2Fixed(nFreeAimX - entity.x[nPlayerEnt]), oF_Whole2Fixed(Weapons[nEquipedWeapon].nFuseLength));
			nDeltaY = oF_divFF(oF_Whole2Fixed(nFreeAimY - entity.y[nPlayerEnt]), oF_Whole2Fixed(Weapons[nEquipedWeapon].nFuseLength));

			if(entity.face[nPlayerEnt] == ENTITY_FACE_UP || entity.face[nPlayerEnt] == ENTITY_FACE_DOWN)
				nThrownAnim = LoadAnimationEX("grenadeUD.png", 8, 8, entity.x[nPlayerEnt], entity.y[nPlayerEnt], "T1 F0 W20 F1 W20 R");
			else
				nThrownAnim = LoadAnimationEX("grenadeLR.png", 8, 8, entity.x[nPlayerEnt], entity.y[nPlayerEnt], "T1 F0 W20 F1 W20 R");

			CreateThrown(oF_Whole2Fixed(entity.x[nPlayerEnt]), oF_Whole2Fixed(entity.y[nPlayerEnt],) nDeltaX, nDeltaY, nThrownAnim, Weapons[nEquipedWeapon].nFuseLength, nEquipedWeapon, Weapons[nEquipedWeapon].sDetFunction);
			
			nAmmo[nEquipedWeapon]--;

			UnpressButtons();
		}
	}
}