//effectFadeIn.vc
//Fades the screen to a color. Only one fade can be active at a time. If a new one starts while one is active the new one takes over.

int nFadeInDuration;
int nFadeInStart;
int nFadeInStop;
int nFadeInColor;

void FadeInExclusive(int duration, int color)	//fades in but blocks at the same time
{
	FadeIn(duration, color);
	
	SwitchPause();
	
	while(IsHooked("ProcessFadeIn"))
	{
		Render();
		ShowPage();
	}
	
	SwitchPause();
}

void FadeIn(int duration, int color)
{
	if(!IsHooked("ProcessFadeIn"))	//functn iz hookd?
		AddHook("ProcessFadeIn", 1500);	//after everything but the menues and logic
		
	//SwitchPause();	//pause the game to block untill the fade is done
	
	nFadeInStart = systemtime;
	nFadeInStop = systemtime + duration;
	nFadeInDuration = duration;
	nFadeInColor = color;
}

void ProcessFadeIn()
{
	SetLucent((systemtime-nFadeInStart)*100/nFadeInDuration);
	RectFill(0, 0, 320, 240, nFadeInColor, screen);
	SetLucent(0);
	
	if(systemtime > nFadeInStop)
	{
		RemoveHook("ProcessFadeIn");
		//SwitchPause();	//unpause to remove the block
	}
}